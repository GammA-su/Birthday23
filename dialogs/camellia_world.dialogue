~ first
BROKEN DIALOG
=> END

~ minihira_blue
Blue Minihira: Do you know Kassan from Neko Hacker? I would really like to get her signature...
do try_start_quest("minihira_blue")
=> END

~ minihira_orange
Red Minihira: There is not enough fire around here, look at all these burned out lanterns! Here, have some matches.
do try_start_quest("minihira_orange")
=> END

~ minihira_green
if has_pizza:
	do try_end_quest("minihira_green")
	Green Minihira: is that pizza with extra pineapple?! [wait=2.0]I want it, give it to me...
	- Feel free to have it all
		set current_npc.dialog_entry = "minihira_green_blocking"
		set minihira_done += 1
		Green Minihira: Since you have a good taste I'm sure you would get along with [b]Fullflavor Girl[/b] behind me.
		if minihira_done < 4:
			Green Minihira: However the other Minihiras don't think you are worth it yet, so I can't let you through
		else:
			Green Minihira: All the Minihiras agree, so feel free to talk to her any time.
	=> END
else:
	Green Minihira: I wanna eat pizza~
	do try_start_quest("minihira_green")
=> END

~ minihira_yellow
Minihira: Nobody has beat my record at drumming yet! Nobody ever will, not without knowing about the secret power-up, fufufu~
do try_start_quest("minihira_yellow")
=> END

~ minihira_blue_blocking
Blue Minihira: I can't let you through, Red Minihira tells me not to!
=> END
~ minihira_orange_blocking
Red Minihira: I can't let you through, Green Minihira tells me not to!
=> END
~ minihira_green_blocking
Green Minihira: I can't let you through, Yellow Minihira tells me not to!
=> END
~ minihira_yellow_blocking
Yellow Minihira: I can't let you through, Blue Minihira tells me not to!
=> END

~ drums_minigame
Shopkeeper: Hello! Would you like to beat the fast drumming highscore?
- Sure
	set get_node("DrumsMinigame").start()
	=> END
- No, thanks
	=> END

~ pizza
Shady Pizza Girl: do you want a totally legal pizza?
- Uhh, "totally legal"?
	Not Forbidden Pizza Girl: well it's not forbidden, that's for sure. The thing is just that I lost the ingredients...
	- Umm... so no pizza?
		=> pizza3
	- Too bad, have a nice day.
		do current_npc.dialog_entry = "pizza2"
		=> END
- No...
	=> END

~ pizza2
Forgetful Pizza Girl: hey, I didn't mean it like that! I meant I just forgot the ingredients... at home, yes, at home!
=> pizza3

~ pizza3
Pizza Girl: I can't leave in case customers show up! Can you get some new ingredients for me instead?
- Sure...
	=> pizza_accept
- That doesn't make any sense!
	Pizza Girl: that's not a no, so...
	=> pizza_accept

~ pizza_accept
Pizza Girl: so the ingredients that are missing are:\n- Pizza dough\n- Pineapples
- Pineapple on Pizza?!
	Pizza Girl: it's delicious!
	=> pizza_accept2
- I mean if I get my pizza...
	=> pizza_accept2

~ pizza_accept2
Pizza Girl: I'll even give you the pizza for free
=> pizza_quest

~ pizza_quest
if is_quest_complete("camellia_collect_dough") and is_quest_complete("camellia_collect_pineapples"):
	do end_quest("camellia_collect_dough")
	do end_quest("camellia_collect_pineapples")
	set current_npc.dialog_entry = "pizza_done"
	=> pizza_done
else
	set current_npc.dialog_entry = "pizza_quest"
	do try_start_quest("camellia_collect_dough")
	Pizza Girl: So, I was pretty sure I had the [b]Pizza dough[/b] somewhere, but I can't seem to find it. It should still be somewhere around here.
	do try_start_quest("camellia_collect_pineapples")
	Pizza Girl: For the [b]Pineapples[/b], some rude customer threw my Pineapples all the way over there onto these stone pillars. Since you got wings, can you maybe fly up there and grab them?
=> END

~ pizza_done
set current_npc.dialog_entry = "pizza_done2"
Pizza Girl: Thank you so much for the ingredients!
Pizza Girl: I'll make you a pizza right away...
Microwave: *bzzzzt* [wait=2.0][do SFX.play_wav("res://sfx/pickup_coin.wav")]*DING*
Pizza Girl: Here you go! Pizza with extra pineapple, for free, as promised.
set has_pizza = true
=> END

~ pizza_done2
Pizza Girl: I've got pizza again, but nobody wants any... More for me!
=> END
